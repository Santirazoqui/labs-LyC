PDefs.   Program ::= [Def] ;

DFun.     Def    ::= Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DInline.  Def    ::= "inline" Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DInlineP. Def    ::= "inline" Type Id "(" [Arg] ")" ";" ;
DProto.   Def    ::= Type Id "(" [Arg] ")" ";" ;
DDcls.    Def    ::= Type IdList ";" ;
DStruct.  Def    ::= "struct" Id "{" [Member] "}" ";" ;
DTypedef. Def    ::= "typedef" Type Id ";" ;
DDeclAny. Def    ::= TypeRef ListDItem ";" ;

terminator Def "" ;

ARaw.       Arg    ::= TypeRef ;
AWithId.    Arg    ::= TypeRef Id ;
AWithInit.  Arg    ::= TypeRef Id "=" Exp ;
separator Arg "," ;

DItemInit. DItem ::= Id "=" Exp ;
DItemId.   DItem ::= Id ;
LDeclOne.  ListDItem ::= DItem ;
LDeclMore. ListDItem ::= DItem "," ListDItem ;

SEmpty.      Stm ::= ";" ;
SExp.        Stm ::= Exp ";" ;
SDeclAny.    Stm ::= TypeRef ListDItem ";" ;
SReturn.     Stm ::= "return" Exp ";" ;
SReturnVoid. Stm ::= "return" ";" ;
SWhile.      Stm ::= "while" "(" Exp ")" Stm ;
SDoWhile.    Stm ::= "do" Stm "while" "(" Exp ")" ;
SBlock.      Stm ::= "{" [Stm] "}" ;
SIfElse.     Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
SIf.         Stm ::= "if" "(" Exp ")" Stm ;
SStruct.     Stm ::= "struct" Id "{" [Member] "}" ";" ;
SFor.        Stm ::= "for" "(" ForInit Exp ";" Exp ")" Stm ;
SForInit.    Stm ::= "for" "(" Exp3 "=" Exp2 ";" Exp ";" Exp ")" Stm ;
FInitAny.    ForInit ::= TypeRef ListDItem ";" ;
terminator Stm "" ;

MDecl.   Member ::= Type IdList ";" ;
terminator Member "" ;

ETrue.    Exp16 ::= "true" ;
EFalse.   Exp16 ::= "false" ;
EInt.     Exp16 ::= Integer ;
EDouble.  Exp16 ::= Double ;
EString.  Exp16 ::= StrLit ;
EChar.   Exp16  ::= Char ;
SOne.     StrLit ::= String ;
SCat.     StrLit ::= StrLit String ;
--ECharLt.  Exp15 ::= "'<'" ;
--ECharGt.  Exp15 ::= "'>'" ; No hay chance que esto este bien
EId.      Exp16 ::= QName ;

ECall.    Exp15 ::= Exp15 "(" [Exp] ")" ;
EIndex.   Exp15 ::= Exp15 "[" Exp "]" ;


EAttr.    Exp14 ::= Exp14 "." Exp15 ;
EArrow.   Exp14 ::= Exp14 "->" Exp15 ;
EPIncr.   Exp14 ::= Exp15 "++" ;
EPDecr.   Exp14 ::= Exp15 "--" ;
EDeref.   Exp14 ::= "*"  Exp15 ;

EIncr.    Exp13 ::= "++" Exp14 ;
EDecr.    Exp13 ::= "--" Exp14 ;
ENot.     Exp13 ::= "!"  Exp14 ;

ETimes.   Exp12 ::= Exp12 "*"  Exp13 ;
EDiv.     Exp12 ::= Exp12 "/"  Exp13 ;
EMod.     Exp12 ::= Exp12 "%"  Exp13 ;

EPlus.    Exp11 ::= Exp11 "+"  Exp12 ;
EMinus.   Exp11 ::= Exp11 "-"  Exp12 ;

ELShift.  Exp10 ::= Exp10 "<<" Exp11 ;
ERShift.  Exp10 ::= Exp10 ">>" Exp11 ;

ELt.      Exp9  ::= Exp9  "<"  Exp10 ;
EGt.      Exp9  ::= Exp9  ">"  Exp10 ;
ELtEq.    Exp9  ::= Exp9  "<=" Exp10 ;
EGtEq.    Exp9  ::= Exp9  ">=" Exp10 ;
EEq.      Exp8  ::= Exp8  "==" Exp9 ;
ENEq.     Exp8  ::= Exp8  "!=" Exp9 ;
EAnd.     Exp4  ::= Exp4  "&&" Exp5 ;
EOr.      Exp3  ::= Exp3  "||" Exp4 ;
ECond.    Exp2  ::= Exp3 "?" Exp ":" Exp2 ;
EAss.     Exp1  ::= Exp2 "="  Exp1 ;
EAssAdd.  Exp1  ::= Exp2 "+=" Exp1 ;
EAssSub.  Exp1  ::= Exp2 "-=" Exp1 ;
EThrow.  Exp1   ::= "throw" Exp2 ;

internal ETyped. Exp16 ::= "(" Exp ":" Type ")" ;
coercions Exp 16 ;
separator Exp "," ;

QId.        QName ::= Id ;
--QQual.      QName ::= Id "::" QName ;
QTempl.     QName ::= Id "<" [Type] ">" ;
--QTemplQual. QName ::= Id "<" [Type] ">" "::" QName ;

rules ConstantName ::= QName ;
rules PrimitiveType   ::= "bool" | "int" | "double" | "void" ;

rules SimpleType ::= ConstantName | PrimitiveType ;

rules  Type ::= SimpleType | "const" SimpleType ;
rules   TypeRef ::= Type | Type "&" ;

separator nonempty Type "," ;

token Id (letter (letter | digit | '_')*) ;
separator nonempty Id "," ;

ILOne.   IdList ::= Id ;
ILMore.  IdList ::= Id "," IdList ;

comment "#" ;
comment "//" ;
comment "/*" "*/" ;
comment "using" ;
